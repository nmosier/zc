; divide unsigned chars
; ARGS:
;  - a: numerator
;  - b: denominator
; RETV:
;  - a: quotient
;  - b: remainder
__bdivu:
   push de
   ld e,c
   ld c,a
   ld d,b
   ld b,8
   xor a,a
__bdivu.loop:
   sla c
   adc a,a
   cp a,d
   jr c,_
   sub a,d
   inc c
_: djnz __bdivu.loop
   ld b,a
   ld a,c
   ld c,e
   pop de
   ret

; convert sign flag (SF) to carry flag (CF)
; DESTROYS: f
___sftocf:
   push bc
   call ___sftocc.base
   rra
   ld a,b
   pop bc
   ret

; convert sign flag (SF) to zero flag (ZF)
; DESTROYS: f
___sftozf:
   push bc
   call ___sftocc.base
   or a,a
   ld a,b
   pop bc
   ret

___sftocc.base:
   ld b,a
   ld a,1
   ret m
   dec a
   ret
