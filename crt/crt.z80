; divide unsigned chars
; ARGS:
;  - a: numerator
;  - b: denominator
; RETV:
;  - a: quotient
;  - b: remainder
__bdivu:
   push de
   ld e,c
   ld c,a
   ld d,b
   ld b,8
   xor a,a
__bdivu.loop:
   sla c
   adc a,a
   cp a,d
   jr c,_
   sub a,d
   inc c
_: djnz __bdivu.loop
   ld b,a
   ld a,c
   ld c,e
   pop de
   ret

;;; FLAG ROUTINES ;;;

; convert sign flag (SF) to carry flag (CF)
; DESTROYS: f
___sftocf:
   push bc
   ld c,$00
   call ____sftocc.base
   rrca
   ld a,b
   pop bc
   ret

; convert sign flag (SF) to carry flag (CF), inverted
; DESTROYS: f
___sftocfn:
   push bc
   ld c,$ff
   call ____sftocc.base
   rrca
   ld a,b
   pop bc
   ret

; convert sign flag (SF) to zero flag (ZF)
; DESTROYS: f
___sftozf:
   push bc
   ld c,$00
   call ____sftocc.base
   or a,a
   ld a,b
   pop bc
   ret

___sftozfn:
   push bc
   ld c,$ff
   call ____sftocc.base
   or a,a
   ld a,b
   pop bc
   ret

____sftocc.base:
   ld b,a
   ld a,c
   ret m
   cpl
   ret

; convert sign flag to bool in char
___sftob:
   ld a,0
   ret p
   inc a
   ret

; convert sign flag to bool, inverted
___sftobn:
   ld a,0
   ret m
   inc a
   ret

; convert sign flag to long
___sftol:
   ld hl,0
   ret p
   inc hl
   ret

; convert sign flag to long, inverted
___sftoln:
   ld hl,0
   ret m
   inc hl
   ret

; invert sign flag
___sfinv:
   push bc
   ld b,a
   ld a,0 
   jp p,_
   cpl
_  or a,a
   ld a,b
   pop bc
   ret